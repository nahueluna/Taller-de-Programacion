programa Pr3_ej5
procesos
  proceso maximo(E quien:numero; E calle:numero; ES maxCa:numero; ES maxRobot:numero)
  comenzar
    si(calle > maxCa)
      maxCa:=calle
      maxRobot:=quien
  fin
  proceso recibirCalle(E quien:numero; ES calle:numero)
  comenzar
    si(quien=1)
      RecibirMensaje(calle, robot1)
    sino
      si(quien=2)
        RecibirMensaje(calle, robot2)
      sino
        si(quien=3)
          RecibirMensaje(calle, robot3)
        sino
          RecibirMensaje(calle, robot4)
  fin
  proceso identificarRobots
  comenzar
    EnviarMensaje(1, robot1)
    EnviarMensaje(2, robot2)
    EnviarMensaje(3, robot3)
    EnviarMensaje(4, robot4)
  fin
  proceso buscarYdejarPapel(E av:numero; E ca:numero; ES continuar:boolean)
  comenzar
    BloquearEsquina(11, 11)
    Pos(11, 11)
    si(HayPapelEnLaEsquina)
      tomarPapel
      Pos(av, ca)
      depositarPapel
      mover
    sino
      continuar:=F
      Pos(av, ca)
    LiberarEsquina(11, 11)
  fin
areas
  AreaJefe: AreaP(1, 1, 1, 1)
  AreaPapeles: AreaPC(11, 11, 11, 11)
  Area1: AreaP(4, 1, 4, 100)
  Area2: AreaP(6, 1, 6, 100)
  Area3: AreaP(8, 1, 8, 100)
  Area4: AreaP(10, 1, 10, 100)
robots
  robot jefe
  variables
    quien, calle, maxCa, maxRobot:numero
  comenzar
    maxCa:=-1
    identificarRobots
    repetir 4
      RecibirMensaje(quien, *)
      recibirCalle(quien, calle)
      maximo(quien, calle, maxCa, maxRobot)
    Informar('Ganador',maxRobot)
  fin
  robot corredor
  variables
    quienSoy, avActual, caActual:numero
    continuar:boolean
  comenzar
    continuar:=V
    RecibirMensaje(quienSoy, robotJefe)
    avActual:=PosAv
    caActual:=PosCa
    mientras(continuar) & (caActual < 100)
      buscarYdejarPapel(avActual, caActual, continuar)
      avActual:=PosAv
      caActual:=PosCa
    EnviarMensaje(quienSoy, robotJefe)
    EnviarMensaje(caActual, robotJefe)
  fin
variables
  robotJefe: jefe
  robot1: corredor
  robot2: corredor
  robot3: corredor
  robot4: corredor
comenzar
  AsignarArea(robotJefe, AreaJefe)
  AsignarArea(robot1, Area1)
  AsignarArea(robot1, AreaPapeles)
  AsignarArea(robot2, Area2)
  AsignarArea(robot2, AreaPapeles)
  AsignarArea(robot3, Area3)
  AsignarArea(robot3, AreaPapeles)
  AsignarArea(robot4, Area4)
  AsignarArea(robot4, AreaPapeles)
  Iniciar(robotJefe, 1, 1)
  Iniciar(robot1, 4, 1)
  Iniciar(robot2, 6, 1)
  Iniciar(robot3, 8, 1)
  Iniciar(robot4, 10, 1)
fin